buildscript {
	repositories {
		jcenter()
		maven { 
			name = "forge"
			url = "http://files.minecraftforge.net/maven" 
		}
	}
	dependencies {
		classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
	}
}

apply plugin: 'java'
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'maven'


compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

sourceCompatibility = 1.8
targetCompatibility = 1.8


//thanks cofh 
ext.configFile = file "build.properties"

configFile.withReader {
	def prop = new Properties()
	prop.load(it)
	project.ext.config = new ConfigSlurper().parse prop
}

def build_number = 'dev'
if (project.hasProperty("release"))
    build_number = 'release'

if (System.getenv('BUILD_NUMBER') != null)
	build_number = System.getenv('BUILD_NUMBER')


//version= 1.12-1.0.0-143-universal.jar
//or in dev= 1.12-1.0.0-dev-universal.jar
version = "${project.config.mc_version}-${project.config.mod_version}-${build_number}"

group = "thundr"
archivesBaseName = "RedstoneRepository"
//TODO: fix this to read from vars
println config.mc_version + "-" + config.forge_version


 repositories {
	 mavenCentral()
	 maven {
 		name = "CoFH Maven"
 		url = "http://maven.covers1624.net"
 	}
 	maven {
 		name = "Thiakil Maven"
 		url = "http://maven.thiakil.com"
 	}
 	maven {
 		name = "JEI Maven"
 		url = "http://dvs1.progwml6.com/files/maven"
 	}
	maven {
		name = "ChickenBones Maven"
		url = "http://chickenbones.net/maven/"
	}
 }

 dependencies {
    deobfCompile "com.azanor.baubles:Baubles:1.12-1.5.2"
	deobfCompile "cofh:RedstoneFlux:${config.base_mc_version}-${config.rf_version}:universal"
    compile "cofh:RedstoneArsenal:${config.mc_version}-${config.ra_version}:deobf"
    compile "cofh:CoFHCore:${config.mc_version}-${config.cofh_core_version}:deobf"
    compile "cofh:CoFHWorld:${config.mc_version}-${config.cofh_world_version}:deobf"
 	compile "codechicken:CodeChickenLib:${config.ccl_mc_version}-${config.ccl_version}:deobf"
    runtime "cofh:ThermalExpansion:${config.mc_version}-${config.te_version}:universal"
    runtime "cofh:ThermalFoundation:${config.mc_version}-${config.tf_version}:universal"
 }

minecraft {
	version = "${config.mc_version}-${config.forge_version}"
	runDir = "run"
	mappings = config.mappings
	replace '${config.mod_version}', project.config.mod_version
	makeObfSourceJar = false
	
}

processResources {
	inputs.property "version", project.version
	inputs.property "mc_version", project.config.mc_version

	from(sourceSets.main.resources.srcDirs) {
		include '**/*.info'
		include '**/*.properties'

		expand 'version': project.version, 'mc_version': project.config.mc_version
	}
	from(sourceSets.main.resources.srcDirs) {
		exclude '**/*.info'
		exclude '**/*.properties'
	}
}

jar {
    classifier = 'universal'
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
}

task srcJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

tasks.build.dependsOn srcJar, deobfJar

idea {
	module {
		inheritOutputDirs = true
	}
}
//testing
task wrapper(type: Wrapper) {
  gradleVersion = '2.6'
}

artifacts {
	archives srcJar
	archives deobfJar
}

task("uploadJars", dependsOn: "build") {
	description = "uploads JARs"
	if (project.hasProperty("local_maven")) {
		apply plugin: 'maven'
		uploadArchives {
			repositories {
				mavenDeployer {
					repository(url: "file://${local_maven}")
					pom {
						artifactId = project.archivesBaseName
					}
				}
			}
		}
	}
}